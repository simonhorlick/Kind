// definition looks up what appears in the source file `uri` at the given
// `offset`. If there is a Fm.Term.ref spanning that location then the
// definition of that term is returned.
Lsp.definition(
  uri: String,
  offset: Nat,
  defs: Fm.Defs
): Maybe(Fm.Def)
  let ds = Lsp.defs(defs, uri)

  // For each def in `uri` find all the definitions that it references.
  let references = List.flatten!(List.map!!((x) Lsp.refs(x), ds))

  // Find references that span the requested offset.
  let matches = List.filter!((x)
    // offset >= x.range.fst && offset <= x.range.snd,
    open x
    Bool.and(
      Nat.gte(offset, Pair.fst!!(x.range)),
      Nat.lte(offset, Pair.snd!!(x.range))),
    references)

  case matches {
    nil : Maybe.none!,
    cons:
      // Take the first match.
      open matches.head as ref
      
      Fm.get!(Fm.Name.read(ref.name), defs)
  }